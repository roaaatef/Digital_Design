import shared_pkg::*;

module ALSU(alsu_if.DUT a_if);
reg red_op_A_reg, red_op_B_reg, bypass_A_reg, bypass_B_reg, direction_reg, serial_in_reg;
reg  [1:0] cin_reg;
reg [2:0] opcode_reg;
reg signed [2:0] A_reg, B_reg;
wire invalid_red_op, invalid_opcode, invalid;
//Invalid handling
assign invalid_red_op = (red_op_A_reg || red_op_B_reg) & (opcode_reg[1] || opcode_reg[2]);
assign invalid_opcode = opcode_reg[1] & opcode_reg[2];
assign invalid = invalid_red_op || invalid_opcode;
//Registering input signals
always @(posedge a_if.clk or posedge a_if.rst) begin
  if(a_if.rst) begin
     cin_reg <= 0;
     red_op_B_reg <= 0;
     red_op_A_reg <= 0;
     bypass_B_reg <= 0;
     bypass_A_reg <= 0;
     direction_reg <= 0;
     serial_in_reg <= 0;
     opcode_reg <= 0;
     A_reg <= 0;
     B_reg <= 0;
  end else begin
     cin_reg <= a_if.cin;
     red_op_B_reg <= a_if.red_op_B;
     red_op_A_reg <= a_if.red_op_A;
     bypass_B_reg <= a_if.bypass_B;
     bypass_A_reg <= a_if.bypass_A;
     direction_reg <= a_if.direction;
     serial_in_reg <= a_if.serial_in;
     opcode_reg <= a_if.opcode;
     A_reg <= a_if.A;
     B_reg <= a_if.B;
  end
end

//leds output blinking 
always @(posedge a_if.clk or posedge a_if.rst) begin
  if(a_if.rst) begin
     a_if.leds <= 0;
  end else begin
      if (invalid)
        a_if.leds <= ~a_if.leds;
      else
        a_if.leds <= 0;
  end
end

//ALSU output processing
always @(posedge a_if.clk or posedge a_if.rst) begin
  if(a_if.rst) begin
    a_if.out <= 0;
  end
  else begin
    if (bypass_A_reg && bypass_B_reg)
      a_if.out <= (INPUT_PRIORITY == "A")? A_reg: B_reg;
    else if (bypass_A_reg)
      a_if.out <= A_reg;
    else if (bypass_B_reg)
      a_if.out <= B_reg;
    else if (invalid) 
        a_if.out <= 0;
    else begin
        case ( opcode_reg)
          3'h0: begin 
            if (red_op_A_reg && red_op_B_reg)
              a_if.out <= (INPUT_PRIORITY == "A")? |A_reg: |B_reg;
            else if (red_op_A_reg) 
              a_if.out <= |A_reg;
            else if (red_op_B_reg)
              a_if.out <= |B_reg;
            else 
              a_if.out <= A_reg | B_reg;
          end
          3'h1: begin
            if (red_op_A_reg && red_op_B_reg)
              a_if.out <= (INPUT_PRIORITY == "A")? ^A_reg: ^B_reg;
            else if (red_op_A_reg) 
              a_if.out <= ^A_reg;
            else if (red_op_B_reg)
              a_if.out <= ^B_reg;
            else 
              a_if.out <= A_reg ^ B_reg;
          end
          3'h2: begin
            if(FULL_ADDER=="ON")
          a_if.out <= A_reg + B_reg+cin_reg;
        else
           a_if.out <= A_reg + B_reg;
       end
          3'h3: a_if.out <= A_reg * B_reg;
          3'h4: begin
            if (direction_reg)
              a_if.out <= {a_if.out[4:0], serial_in_reg};
            else
              a_if.out <= {serial_in_reg, a_if.out[5:1]};
          end
          3'h5: begin
            if (direction_reg)
              a_if.out <= {a_if.out[4:0], a_if.out[5]};
            else
              a_if.out <= {a_if.out[0], a_if.out[5:1]};
          end
        endcase
    end 
  end
end
endmodule


